---
id: 2
title: "ê°œì¸ ë¸”ë¡œê·¸ì— í•„ìš”í•œ AWS ì¸í”„ë¼ ëª‡ ë¶„ë§Œì— ë°°í¬í•˜ê¸°(with Terraform)"
date: "2025-05-02"
summary: "í˜„ì¬ ì œ ê°œì¸ ë¸”ë¡œê·¸ì˜ ì¸í”„ë¼ë¥¼ Terraformì„ ì´ìš©í•˜ì—¬ ë°°í¬í•œ í¬ìŠ¤íŠ¸ ì…ë‹ˆë‹¤."
thumbnail: "/images/DevOps/Terraform/my-blog-infrastructure-with-terraform/thumbnail-my-architecture.png"
tags: ["Terraform", "AWS", "Blog"]
---

# Terraformìœ¼ë¡œ ê°œì¸ ë¸”ë¡œê·¸ ì¸í”„ë¼ êµ¬ì¶•í•˜ê¸°

ì•ˆë…•í•˜ì„¸ìš”! ì´ ê¸€ì€ ê°œì¸ ë¸”ë¡œê·¸ë¥¼ ìš´ì˜í•˜ë©´ì„œ í•„ìš”í•œ AWS ì¸í”„ë¼ë¥¼ Terraformìœ¼ë¡œ êµ¬ì„±í•œ ê³¼ì •ì„ ì •ë¦¬í•œ í¬ìŠ¤íŠ¸ì…ë‹ˆë‹¤. ì½”ë“œë¡œ ì¸í”„ë¼ë¥¼ ê´€ë¦¬í•˜ëŠ” ë°©ì‹(IaC: Infrastructure as Code)ì— ê´€ì‹¬ ìˆìœ¼ì‹  ë¶„ë“¤ì´ ì°¸ê³ í•˜ê¸° ì¢‹ì€ ì‹¤ì „ ì˜ˆì‹œì…ë‹ˆë‹¤.

# ì¸í”„ë¼ êµ¬ì„± ë°°ê²½

ì œ ê°œì¸ ë¸”ë¡œê·¸ëŠ” Next.js ê¸°ë°˜ì´ë©°, output: "export" ëª¨ë“œë¥¼ ì‚¬ìš©í•´ ì •ì  ì‚¬ì´íŠ¸ë¡œ ë¹Œë“œë©ë‹ˆë‹¤. ì¦‰, ì›¹ ì„œë²„ ì—†ì´ë„ ì •ì  ë¦¬ì†ŒìŠ¤ë§Œìœ¼ë¡œ ì„œë¹„ìŠ¤ë¥¼ ìš´ì˜í•  ìˆ˜ ìˆëŠ” êµ¬ì¡°ì…ë‹ˆë‹¤.

ê·¸ë˜ì„œ ì €ëŠ” ë‹¤ìŒê³¼ ê°™ì€ AWS ì¸í”„ë¼ë¥¼ êµ¬ì„±í•˜ê¸°ë¡œ í–ˆìŠµë‹ˆë‹¤:

![ì œ ë¸”ë¡œê·¸ì˜ AWS ì¸í”„ë¼ ì•„í‚¤í…ì²˜](/images/DevOps/Terraform/my-blog-infrastructure-with-terraform/0368f2aa-e185-4a86-898b-68fc557a653e-my-architecture.png)

- **S3**: ì •ì  ë¦¬ì†ŒìŠ¤ ì €ì¥
- **Cloudfront**: ì „ ì„¸ê³„ ë¹ ë¥¸ ë°°í¬ì™€ ìºì‹±
- **Route 53**: ë„ë©”ì¸ ì—°ê²° ë° DNS ê´€ë¦¬

# Terraform ì‘ì„±

> [Terraformì€](https://developer.hashicorp.com/terraform/intro) ì¸í”„ë¼ë¥¼ ì½”ë“œë¡œ ì„ ì–¸í•˜ê³  ê´€ë¦¬í•  ìˆ˜ ìˆê²Œ ë„ì™€ì£¼ëŠ” ë„êµ¬ì…ë‹ˆë‹¤. ì½”ë“œë¡œ ì„ ì–¸ëœ ë¦¬ì†ŒìŠ¤ëŠ” ì¬í˜„ì„±ê³¼ ë²„ì „ ê´€ë¦¬ê°€ ì‰¬ì›Œì§‘ë‹ˆë‹¤.

## Provider êµ¬ì„±

```json title="/provider.tf"
terraform {
  required_providers {
    aws = {
      source  = "hashicorp/aws"
      version = "~> 4.16"
    }
  }

  required_version = ">= 1.2.0"
}

provider "aws" {
  region  = "ap-northeast-2"
  profile = var.profile
}
```

:::info
AWS ë¦¬ì „ì€ ì„œìš¸(ap-northeast-2)ë¡œ ì„¤ì •í–ˆê³ , ë¡œì»¬ì˜ AWS CLI ì¸ì¦ í”„ë¡œíŒŒì¼ì„ ì‚¬ìš©í•©ë‹ˆë‹¤.
:::

## S3 êµ¬ì„±

```json title="/s3.tf"
resource "aws_s3_bucket" "my_website" {
  bucket = var.bucket_name

  tags = {
    Name        = var.bucket_name
    Environment = "Production"
  }
}

resource "aws_s3_bucket_public_access_block" "my_bucket_acl" {
  bucket = aws_s3_bucket.my_website.id

  block_public_acls       = true
  block_public_policy     = true
  ignore_public_acls      = true
  restrict_public_buckets = true
}

resource "aws_s3_bucket_policy" "my_website_policy" {
  bucket = aws_s3_bucket.my_website.id

  policy = jsonencode({
    Version = "2012-10-17",
    Statement = [
      {
        Effect = "Allow",
        Principal = {
          Service = "cloudfront.amazonaws.com"
        },
        Action   = "s3:GetObject",
        Resource = "arn:aws:s3:::${var.bucket_name}/*",
        Condition = {
          StringEquals = {
            "AWS:SourceArn" = "arn:aws:cloudfront::${data.aws_caller_identity.current.account_id}:distribution/${aws_cloudfront_distribution.cdn.id}"
          }
        }
      }
    ]
  })

  depends_on = [aws_s3_bucket_public_access_block.my_bucket_acl]
}
```

S3 ë²„í‚·ì„ ìƒì„±í•˜ì—¬, í¼ë¸”ë¦­ ì ‘ê·¼ì„ ì°¨ë‹¨í•˜ê³  CloudFrontì—ì„œë§Œ ì ‘ê·¼í•  ìˆ˜ ìˆë„ë¡ OAC(Origin Access Control) ì •ì±…ì„ ì ìš©í•©ë‹ˆë‹¤.

## Cloudfront êµ¬ì„±

```json title="/cloudfront.tf"
resource "aws_cloudfront_origin_access_control" "oac" {
  name                              = "oac-for-${var.bucket_name}"
  origin_access_control_origin_type = "s3"
  signing_behavior                  = "always"
  signing_protocol                  = "sigv4"
}

resource "aws_cloudfront_cache_policy" "my_custom_cache_policy" {
  name        = "s3-website-cache-policy"
  comment     = "Website cache policy"
  default_ttl = 3600
  max_ttl     = 43200
  min_ttl     = 3600
  parameters_in_cache_key_and_forwarded_to_origin {
    cookies_config {
      cookie_behavior = "none"
    }
    headers_config {
      header_behavior = "whitelist"
      headers {
        items = ["Authorization", "Origin"]
      }
    }
    query_strings_config {
      query_string_behavior = "none"
    }
  }
}

resource "aws_cloudfront_function" "my_cloudfront_function" {
  name    = "my-website-routing-function"
  runtime = "cloudfront-js-1.0"
  comment = "Function to handle my website routing"
  publish = true
  code    = file("${path.module}/function.js")
}

resource "aws_cloudfront_distribution" "cdn" {
  origin {
    domain_name = aws_s3_bucket.my_website.bucket_regional_domain_name
    origin_id   = "S3-${var.bucket_name}"

    origin_access_control_id = aws_cloudfront_origin_access_control.oac.id

    origin_shield {
      enabled = true
      origin_shield_region = var.region
    }
  }

  enabled             = true
  is_ipv6_enabled     = false
  comment             = "CloudFront Distribution for ${var.bucket_name}"
  aliases             = ["blog.${var.domain_name}"]

  price_class = "PriceClass_200"

  viewer_certificate {
    acm_certificate_arn      = "arn:aws:acm:us-east-1:${data.aws_caller_identity.current.account_id}:certificate/${var.acm_id}"
    ssl_support_method       = "sni-only"
    minimum_protocol_version = "TLSv1.2_2021"
  }

  default_cache_behavior {
    target_origin_id       = "S3-${var.bucket_name}"
    viewer_protocol_policy = "redirect-to-https"

    allowed_methods = ["GET", "HEAD"]
    cached_methods  = ["GET", "HEAD"]

    cache_policy_id = aws_cloudfront_cache_policy.my_custom_cache_policy.id
    origin_request_policy_id   = "88a5eaf4-2fd4-4709-b370-b4c650ea3fcf" # managed cors_s3_id
    response_headers_policy_id = "5cc3b908-e619-4b99-88e5-2cf7f45965bd" # managed cors_with_preflight_id

    function_association {
      event_type   = "viewer-request"
      function_arn = aws_cloudfront_function.my_cloudfront_function.arn
    }
  }

  custom_error_response {
    error_code            = 403
    response_page_path    = "/404.html"
    response_code         = 200
    error_caching_min_ttl = 2
  }

  restrictions {
    geo_restriction {
      restriction_type = "none"
    }
  }
}
```

Cloudfront ë°°í¬ë¥¼ ìƒì„±í•˜ê³ , Cloudfront function, OAC ì„¤ì •ì„ í–ˆìŠµë‹ˆë‹¤.

:::info
CloudFrontì™€ S3ë¥¼ ì•ˆì „í•˜ê²Œ ì—°ê²°í•˜ë ¤ë©´ OAC ì„¤ì •ì´ í•„ìš”í•©ë‹ˆë‹¤. OAIëŠ” êµ¬ë²„ì „ì´ë¯€ë¡œ ê°€ê¸‰ì  OACë¥¼ ì‚¬ìš©í•˜ëŠ” ê²Œ ìµœì‹  ê°€ì´ë“œì— ë¶€í•©í•©ë‹ˆë‹¤.
:::

```json title="/function.js"
var regexExpr = /^\/.+(\.\w+$)/;

function handler(event) {
  var request = event.request;
  var olduri = request.uri;

  if (!regexExpr.test(olduri)) {
    request.uri = olduri.replace(/\/?$/, '/') + 'index.html';
    console.log('Request for [' + olduri + '] rewritten to [' + request.uri + ']');
  }

  return request;
}
```

:::info
ìœ„ í•¨ìˆ˜ëŠ” /blog, /about ê°™ì€ ê²½ë¡œ ìš”ì²­ì„ /blog/index.html, /about/index.htmlë¡œ rewrite í•´ì¤ë‹ˆë‹¤. ì´ëŠ” ì •ì  ì‚¬ì´íŠ¸ í˜¸ìŠ¤íŒ…ì„ ìœ„í•´ í•„ìš”í•œ ì„¤ì •ìœ¼ë¡œ, Cloudfront functionì— ì‚¬ìš©ë  í•¨ìˆ˜ì…ë‹ˆë‹¤.
:::

## Route 53 êµ¬ì„±

```json title="/route53.tf"
data "aws_route53_zone" "my_route53_zone" {
  name         = var.domain_name
  private_zone = false
}

resource "aws_route53_record" "my_record" {
  zone_id = data.aws_route53_zone.my_route53_zone.zone_id
  name    = "blog.${data.aws_route53_zone.my_route53_zone.name}"
  type    = "A"

  alias {
    name                   = aws_cloudfront_distribution.cdn.domain_name
    zone_id                 = aws_cloudfront_distribution.cdn.hosted_zone_id
    evaluate_target_health  = false
  }
}
```

Route 53ì— CloudFrontë¥¼ ì—°ê²°í•´ ì»¤ìŠ¤í…€ ë„ë©”ì¸(ì˜ˆ: blog.example.com)ìœ¼ë¡œ ì ‘ê·¼í•  ìˆ˜ ìˆë„ë¡ ì„¤ì •í–ˆìŠµë‹ˆë‹¤.

## ë°°í¬ ê³¼ì •

```bash
terraform init
terraform plan
terraform apply
```

- **init**: í•„ìš”í•œ provider ì„¤ì¹˜
- **plan**: ìƒì„±/ë³€ê²½/ì‚­ì œë  ë¦¬ì†ŒìŠ¤ ì‚¬ì „ í™•ì¸
- **apply**: ì‹¤ì œ ì¸í”„ë¼ ì ìš©

## ê²°ê³¼ í™•ì¸

S3, CloudFront, Route 53 ëª¨ë‘ ì •ìƒì ìœ¼ë¡œ ì„¤ì •ëœ ê²ƒì„ ì½˜ì†”ì—ì„œ í™•ì¸í•  ìˆ˜ ìˆìŠµë‹ˆë‹¤.

### S3 í™•ì¸

![S3 ë²„í‚· ìƒì„± í™•ì¸](/images/DevOps/Terraform/my-blog-infrastructure-with-terraform/07b1b354-af6a-43e7-8b22-089301703ce4-SCR-20250502-oers.png)

![OAC ì •ì±… í™•ì¸](/images/DevOps/Terraform/my-blog-infrastructure-with-terraform/f7080732-0640-446a-bc57-ea0f9251cbad-SCR-20250502-ofsj.png)

### Cloudfront í™•ì¸

![Cloudfront ë°°í¬ ìƒì„± í™•ì¸](/images/DevOps/Terraform/my-blog-infrastructure-with-terraform/90954d59-8a5d-459c-bb4a-9d996ab79bfb-SCR-20250502-oeys.png)

![Cloudfront ì›ë³¸ í™•ì¸](/images/DevOps/Terraform/my-blog-infrastructure-with-terraform/039129a9-0b07-4c65-b1e9-13010f112aec-SCR-20250502-oghm.png)

![Cloudfront ìºì‹œ ë™ì‘ í™•ì¸](/images/DevOps/Terraform/my-blog-infrastructure-with-terraform/f630006e-8123-4a75-80c9-0548dce46628-SCR-20250502-ogqx.png)

### Route 53 í™•ì¸

![Route 53 ì„¤ì • í™•ì¸](/images/DevOps/Terraform/my-blog-infrastructure-with-terraform/7555b0c2-c2cf-4f1d-8fba-499f17adbbbe-SCR-20250502-ofmk.png)

# ë§ˆë¬´ë¦¬

Terraformì„ ì´ìš©í•˜ë‹ˆ í´ë¦­ ëª‡ ë²ˆ ì—†ì´ë„ í•„ìš”í•œ ë¦¬ì†ŒìŠ¤ë¥¼ í•œë²ˆì— ê´€ë¦¬í•˜ê³ , ì½”ë“œ ê¸°ë°˜ìœ¼ë¡œ ì¶”ì /ê´€ë¦¬í•  ìˆ˜ ìˆì–´ í¸ë¦¬í–ˆìŠµë‹ˆë‹¤. íŠ¹íˆ ì´í›„ ë¸”ë¡œê·¸ë¥¼ ë§ˆì´ê·¸ë ˆì´ì…˜í•˜ê±°ë‚˜ ë‹¤ë¥¸ í™˜ê²½ì— ì¬êµ¬ì„±í•  ë•Œë„ ì†ì‰½ê²Œ ì¸í”„ë¼ë¥¼ ì¬ì‚¬ìš©í•  ìˆ˜ ìˆëŠ” í° ì¥ì ì´ ìˆìŠµë‹ˆë‹¤.

ë‹¤ìŒì— ê¸°íšŒê°€ ëœë‹¤ë©´, Terraform ê´€ë ¨ í¬ìŠ¤íŠ¸ë¥¼ ë” ì‘ì„±í•´ë³´ë„ë¡ í•˜ê² ìŠµë‹ˆë‹¤.

# ì°¸ê³  ìë£Œ

- ğŸ“ [Terraform ê³µì‹ ë¬¸ì„œ](https://developer.hashicorp.com/terraform/intro)
- ğŸ“˜ [AWS OAC(Origin Access Control) ì†Œê°œ ë¬¸ì„œ](https://docs.aws.amazon.com/AmazonCloudFront/latest/DeveloperGuide/Introduction.html)
- ğŸ§  [CloudFront Functions ê³µì‹ ê°€ì´ë“œ](https://docs.aws.amazon.com/AmazonCloudFront/latest/DeveloperGuide/cloudfront-functions.html)
